Queries
  = query:Query queries:("," Query)* {
    return [query].concat(queries.map(function(x) { return x[1]; }));
  }

Query
  = key:Key? table:Table fields:Fields? options:Options? filters:Filters? {
  	return {
      key: key,
      table: table,
      fields: fields,
      options: options,
      filters: filters
    };
  }

Key
  = name:VariableName ":" { return name; }

Filters
  = "?" expressions:Expression+ {
    return expressions;
  }

Expression
  = head:Term tail:(OrOperator Term)* {
    if (tail.length) {
      return { op : "|", terms: [head].concat(tail.map(function(x) { return x[1]; })) };
    } else {
      return head;
    }
  }

Term
  = head:Factor tail:(AndOperator Factor)* {
    if (tail.length) {
      return { op : "&", terms: [head, tail[0][1]] };
    } else {
      return head;
    }
  }

Factor
  = LeftParen expression:Expression RightParen { return expression; }
  / Statement

AndOperator
  = ("&" / "&&") { return text(); }

OrOperator
  = ("|" / "||") { return text(); }

LeftParen
  = "(" { return text(); }

RightParen
  = ")" { return text(); }

Options
  = head:"[" body:Statement+ tail:"]" {
    return body;
  }

Statement
  = key:Word predicate:(Predicate / Regex) {
    return {
      type: "statement",
      key: key,
      comparator: predicate.comparator,
      value: predicate.value
    }
  }

Predicate
 = comparator:Comparator value:("+" / "-" / "*" / String / Word / Number)+ ","? {
   return {
     value: value.join(""),
     comparator: comparator
   }
 }

Regex
 = "~" expression:RegexExpression flags:([gim]*) {
   return {
     value: expression + flags.join(""),
     comparator: "~"
   }
}

RegexExpression
 = "/" ("\\\/" / [^/])+ "/" { return text() }

Comparator
 = ("=" / "<=" / "<" / ">=" / ">") { return text(); }

Fields
  = head:"{" body:Field+ tail:"}" {
  	return body;
  }

Field
  = word:Word ","? {
    return word;
  }

Table
  = head:"/"? tail:(Word / "/")* {
  	return tail.join("");
  }

Word
  = (Variable / VariableName)

Variable
  = "{{" [^}]+ "}}" { return text(); }

VariableName
  = [a-zA-Z_$][0-9a-zA-Z_$]* { return text(); }

String
  = "\"" [^\"]+ "\"" { return text(); }

Number
  = [0-9]+ "."? [0-9]* { return parseFloat(text()); }
